base {
    archivesName = "${mod_id}-neoforge-${minecraft_version}"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

repositories {
    maven { url "https://maven.neoforged.net/releases/" }
}

dependencies {
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    neoForge "net.neoforged:neoforge:${neoforge_version}"

    compileOnly project(":common")
}

loom {
    if(project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()){
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    neoForge {}

    runs {
        client { client(); setConfigName("NeoForge Client") }
        server { server(); setConfigName("NeoForge Server") }
        datagen {
            data()
            setConfigName("NeoForge Datagen")
            programArgs "--all", "--mod", "${mod_id}"
            programArgs "--output", file("src/main/generated").getAbsolutePath()
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}
processResources {
    from project(":common").sourceSets.main.resources
    from project(":neoforge").file("src/main/generated")
}