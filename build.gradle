plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    configurations {
        common
        compileClasspath.extendsFrom common
        runtimeClasspath.extendsFrom common
        developmentFabric.extendsFrom common
        // don't know why, but using developmentForge results in an Exception but using developmentFabric works fine everytime.
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    processResources {
        def expandProps =[
                // Mod related Keys
                "mod_version": mod_version,
                "maven_group": maven_group,
                "minecraft_version": minecraft_version,
                "mod_name": mod_name,
                "mod_author": mod_author,
                "mod_id": mod_id,
                "mod_license": mod_license,
                "mod_credits": mod_credits,
                "mod_description": mod_description,

                // General Key
                "minecraft_version_range": minecraft_version_range,

                // Fabric Keys
                "fabric_loader_version": fabric_loader_version,
                "fabric_api_version": fabric_api_version,

                // Forge Keys
                "forge_version": forge_version,
                "forge_loader_version_range": forge_loader_version_range,
                "forge_version_range": forge_version_range,

                // NeoForge Keys
                "neoforge_version": neoforge_version,
                "neoforge_loader_version_range": neoforge_loader_version_range
        ]
        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    version = mod_version
    group = maven_group

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 17
    }

    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }
}
